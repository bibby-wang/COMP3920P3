<program> ::= CD19 <id> <globals> <funcs> <mainbody>
<globals> ::= <consts> <types> <arrays>
<consts> ::= constants <initlist> | ε
<initlist> ::= <init> , <initlist> | <init>
<init> ::= <id> = <expr>
<types> ::= types <typelist> | ε
<arrays> ::= arrays <arrdecls> | ε
<funcs> ::= <func> <funcs> | ε
<mainbody> ::= main <slist> begin <stats> end CD19 <id>
<slist> ::= <sdecl> , <slist> | <sdecl>
<typelist> ::= <type> <typelist> | <type>
<type> ::= <structid> is <fields> end | <typeid> is array [ <expr> ] of <structid>
<fields> ::= <sdecl> , <fields> | <sdecl>
<sdecl> ::= <id> : <stype>
<arrdecls> ::= <arrdecl> , <arrdecls> | <arrdecl>
<arrdecl> ::= <id> : <typeid>
<func> ::= function <id> ( <plist> ) : <rtype> <funcbody>
<rtype> ::= <stype> | void
<plist> ::= <params> | ε
<params> ::= <param> , <params> | <param>
<param> ::= <sdecl> | <arrdecl> | const <arrdecl>
<funcbody> ::= <locals> begin <stats> end
<locals> ::= <dlist> | ε
<dlist> ::= <decl> , <dlist> | <decl>
<decl> ::= <sdecl> | <arrdecl>
<stype> ::= integer | real | boolean
<stats> ::= <stat> ; <stats> | <strstat> <stats> | <stat> ; | <strstat>
<strstat> ::= <forstat> | <ifstat>
<stat> ::= <reptstat> | <asgnstat> | <iostat> | <callstat> | <returnstat>
<forstat> ::= for ( <asgnlist> ; <bool> ) <stats> end
<repstat> ::= repeat ( <asgnlist> ) <stats> until <bool>
<asgnlist> ::= <alist> | ε
<alist> ::= <asgnstat> , <alist> | <asgnstat>
<ifstat> ::= if ( <bool> ) <stats> end | if ( <bool> ) <stats> else <stats> end
<asgnstat> ::= <var> <asgnop> <bool>
<asgnop> ::= = | += | -= | *= | /=
<iostat> ::= input <vlist> | print <prlist> | printline <prlist>
<callstat> ::= <id> ( <elist> ) | <id> ( )
<returnstat> ::= return | return <expr>
<vlist> ::= <var> , <vlist> | <var>
<var> ::= <id> | <id> [ <expr> ] . <id>
<elist> ::= <bool> , <elist> | <bool>
<bool> ::= <bool> <logop> <rel>| <rel>
<rel> ::= not <expr> <relop> <expr> | <expr> <relop> <expr> | <expr>
<logop> ::= and | or | xor
<relop> ::= == | != | > | <= | < | >=
<expr> ::= <expr> + <term> | <expr> - <term> | <term>
<term> ::= <term> * <fact> | <term> / <fact> | <term> % <fact> | <fact>
<fact> ::= <fact> ^ <exponent> | <exponent>
<exponent> ::= <var> | <intlit> | <reallit> | <fncall> | true | false | ( <bool> )
<fncall> ::= <id> ( <elist> ) | <id> ( )
<prlist> ::= <printitem> , <prlist> | <printitem>
<printitem> ::= <expr> | <string>
<id> ::= id
<structid> ::= id
<typeid> ::= id
<intlit> ::= 42
<reallit> ::= 0.0000001000000100000110
<string> ::= "oh hai mark"