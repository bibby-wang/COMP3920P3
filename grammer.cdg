NPROG				<program> ::= CD19 <id> <globals> <funcs> <mainbody>
NGLOB 				<globals> ::= <consts> <types> <arrays> 
Special				<consts> ::= constants <initlist> | ¦Å
Special				<initlist> ::= <init> <initlistb>
NILIST				<initlistb> ::= , <initlist> | ¦Å
NINIT				<init> ::= <id> = <expr>
Special				<types> ::= types <typelist> | ¦Å
Special				<arrays> ::= arrays <arrdecls> | ¦Å
NFUNCS				<funcs> ::= <func> <funcs> | ¦Å
NMAIN				<mainbody> ::= main <slist> begin <stats> end CD19 <id>
Special				<slist> ::= <sdecl> <slistb>
NSDLST				<slistb> ::= , <slist> | ¦Å
Special				<typelist> ::= <type> <typelistb>
NTYPEL				<typelistb> ::= <typelist> | ¦Å
NRTYPE	NATYPE		<type> ::= <structid> is <fields> end | <typeid> is array [ <expr> ] of <structid>
Special				<fields> ::= <id> : <stype> <fieldsb>
NFLIST				<fieldsb> ::= , <fields> | ¦Å
NSDECL				<sdecl> ::= <id> : <stype>
Special				<arrdecls> ::= <arrdecl> <arrdeclsb>
NALIST				<arrdeclsb> ::= , <arrdecls> | ¦Å
NARRD				<arrdecl> ::= <id> : <typeid>
NFUND				<func> ::= function <id> ( <plist> ) : <rtype> <locals> begin <stats> end
Special				<rtype> ::= void | <stype> //stRec
Special				<plist> ::= <params> | ¦Å
Special				<params> ::= <param> <paramsb> 
NPLIST				<paramsb> ::= , <params> | ¦Å
NSIMP NARRP NARRC	<param> ::= <decl> | const <arrdecl>
Special				<locals> ::= <dlist> | ¦Å
Special				<dlist> ::= <decl> <dlistb>
NDLIST				<dlistb> ::= , <dlist> | ¦Å
Special				<decl> ::= <id> : <declb>
Special				<declb> ::= <stype> | <typeid>
Special				<stype> ::= integer | real | boolean //stRec
Special				<stats> ::= <stat> ; <statsb> | <strstat> <statsb> 
NSTATS				<statsb> ::= <stat> ; <statsb> | <strstat> <statsb> | ¦Å
Special				<strstat> ::= <forstat> | <ifstat>
Special				<stat> ::= <repstat> | <id> <statb> | <iostat>  | <returnstat>
Special				<statb> ::= <asgnstat> | <callstat> 
NFOR				<forstat> ::= for ( <asgnlist> ; <bool> ) <stats> end
NREPT				<repstat> ::= repeat ( <asgnlist> ) <stats> until <bool>
Special				<asgnlist> ::= <alist> | ¦Å
Special				<alist> ::= <id> <asgnstat> <alistb>
NASGNS				<alistb> ::= <id> <asgnstat> , <alist> | ¦Å
Special				<ifstat> ::= if ( <bool> ) <stats> <ifstatb>
NIFTH NIFTE			<ifstatb> ::= end | else <stats> end
Special				<asgnstat> ::= <var> <asgnop> <bool>
Various				<asgnop> ::= = | += | -= | *= | /=           NASGN NPLEQ NMNEQ NSTEQ NDVEQ
NINPUT NPRINT NPRLN	<iostat> ::= input <vlist> | print <prlist> | printline <prlist>
NCALL				<callstat> ::= ( <callstatb>  
Special				<callstatb> ::= <elist> ) | )
NRETN				<returnstat> ::= return <returnstatb>
Special				<returnstatb> ::= <expr> | ¦Å
Special				<vlist> ::= <id> <var> <vlistb>
NVLIST				<vlistb> ::= , <vlist> | ¦Å
NSIMV NARRV			<var> ::= ¦Å | [ <expr> ] . <id> 
Special				<elist> ::= <bool> <elistb>
NEXPL				<elistb> ::= , <elist> | ¦Å 
Special				<bool> ::=  <rel> <boolb> 
NBOOL				<boolb> ::= <logop> <rel> <boolb> | ¦Å
NNOT				<rel> ::= not <expr> <relop> <expr> | <expr> <relb> 
Special				<relb> ::= <relop> <expr>  | ¦Å
Various				<logop> ::= and | or | xor            NAND NOR NXOR
Various				<relop> ::= == | != | > | <= | < | >=         NEQL NNEQ NGRT
Special				<expr> ::= <term> <exprb>
NADD NSUB			<exprb> ::= + <term> <exprb> | - <term> <exprb> | ¦Å
Special				<term> ::= <fact> <termb>
NMUL NDIV NMOD		<termb> ::= * <fact> <termb> | / <fact> <termb> | % <fact> <termb> | ¦Å
Special				<fact> ::=  <exponent> <factb> 
NPOW				<factb> ::=  ^ <exponent> <factb> | ¦Å
NILIT NFLIT			<exponent> ::= <intlit> | <reallit> | true | false | ( <bool> ) | <id> <exponentb> 
Special				<exponentb> ::= <var> | <fncall>
NFCALL				<fncall> ::=  ( <fncallb> 
Special				<fncallb> ::= <elist> ) | )
Special				<prlist> ::= <printitem> <prlistb>
NPRLST				<prlistb> ::= , <prlist> | ¦Å
NSTRG				<printitem> ::= <string> | <expr> 